# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - swap zstd for gzip in depends, remove gzip optdepend
#  - patch to revert back to gzip for default compression
#  - patch for gzip kernel image support (https://github.com/archlinux/mkinitcpio/pull/86)

pkgname=mkinitcpio
pkgver=39
pkgrel=1
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url='https://gitlab.archlinux.org/archlinux/mkinitcpio/mkinitcpio'
license=('GPL-2.0-only')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'binutils' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
checkdepends=('bats' 'bats-assert' 'lzop')
makedepends=('asciidoc')
checkdepends=('bats' 'bats-assert' 'lzop')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig}
        '0001-use-gzip-for-compression-by-default.patch'
        '0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch'
        '0003-check-for-ALARM-kernels-in-run_post_hooks.patch')
sha512sums=('50ae08ddf7596b821c89f927ff6692ca907a5045590096155ec6de64e0b5da647ff2c75d1603ab0035046096d37a6eacae383d458b1bb36bd525691b2b23c9ea'
            'SKIP'
            'f8110186cb107b21f1234f53aea34aee1906b7f9b663fb212f455d53860debe52d0852abcde9ed7d911cbc36c86ad7213243678c25c517b0b9bcc13bd61ad17b'
            '23dd4a2046bb1370917fff7a4922c287a7314a9dea7623eafad0caba197763bff585d3afa9cea22c5e0d4ee2a21471ce79a8690614905632f4acd4145f314e4e'
            'f6e3f5f8c97002d28a5eb7f9ad4327d76f4f6d1f072cb9f8d3473264424c76eb3417afcc69dae8fd53c9196f47fc2052a9bd2fbbd46eba49d1e6096b8bd119bb')
b2sums=('3a28d6711ce843aafe2868b09ba5a723e57e4fdffe905ac915aa5d224279590a10bba902933f183de33e08e96a6aad4d9e1d4752be74c011a37c4cbe83c8d645'
        'SKIP'
        '04fbfd50700e5108738552a3fa16f891d3eced3a64b076834a2ac71cba45d073b701e0700bd9919be36680ed971a58bbcd93b3f65613eb1e836aaafcc3db37ab'
        '2e5a30cc77f9108ee0f31540d22081817c3c223f5db27e3b9ae1bb8625f7ae2689bea4300872fdf646f8b19df7d8f3aff71790b1968a2a2e208dcb75a185d73e'
        'e10126da8fb8d47217b56fe9af72c1b16ea6940465081811d2ef55c21b64c67c72a2d2a565728dd9ed6ab06e4bfc62952f202b2b3dce2a3db046f8961f1c6f39')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              'C100346676634E80C940FB9E9C02FF419FECBE16')   # Morten Linderud

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../0001-use-gzip-for-compression-by-default.patch
  patch -p1 -i ../0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch
  patch -p1 -i ../0003-check-for-ALARM-kernels-in-run_post_hooks.patch
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
