# Maintainer: graysky <therealgraysky AT protonmail DOT com>

pkgname=raspberrypi-utils
pkgver=20250303
pkgrel=1
_commit=660bba39805585fb39d37d49b8268073932696bb
pkgdesc="Legacy scripts and simple applications for Raspberry Pi"
arch=('aarch64' 'armv7h')
url="https://github.com/raspberrypi/utils"
license=('custom')
makedepends=('cmake' )
depends=('dtc')
replaces=('raspberrypi-firmware')
conflicts=('raspberrypi-firmware')
options=(!strip)
source=("utils-$pkgver-$pkgrel-${_commit:0:10}.tar.gz::https://github.com/raspberrypi/utils/archive/$_commit.tar.gz"
         10-raspberrypi-utils.rules
)
md5sums=('471ef5a86aba76d99b3e21a9982a2605'
         'a0423d9850cc56a15967e103c351c335')

build() {
  cd "utils-$_commit"
  cmake -S . -B . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "utils-${_commit}"
  make install DESTDIR="$pkgdir"
  install -Dm0644 LICENCE -t "$pkgdir/usr/share/licenses/$pkgname"

  # setup permissions on video group for /usr/bin/vcgencmd
  install -Dm0644 ../10-raspberrypi-utils.rules "$pkgdir/usr/lib/udev/rules.d/10-raspberrypi-utils.rules"
}
