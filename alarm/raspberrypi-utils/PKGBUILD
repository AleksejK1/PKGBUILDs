# Maintainer: graysky <therealgraysky AT protonmail DOT com>

pkgname=raspberrypi-utils
pkgver=20231129
pkgrel=1
_commit=64a4d7083a5d9c9be49ea56a87077cb81644eeb2
pkgdesc="Legacy scripts and simple applications for Raspberry Pi"
arch=('aarch64' 'armv7h')
url="https://github.com/raspberrypi/utils"
license=('custom')
makedepends=('cmake' 'dtc')
replaces=('raspberrypi-firmware')
conflicts=('raspberrypi-firmware')
options=(!strip)
source=("utils-$pkgver-$pkgrel-${_commit:0:10}.tar.gz::https://github.com/raspberrypi/utils/archive/$_commit.tar.gz"
10-raspberrypi-utils.rules)
md5sums=('e088336110b148284385ec3d55b55aad'
         'a0423d9850cc56a15967e103c351c335')

build() {
  cd "utils-$_commit"
  cmake -S . -B . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "utils-${_commit}"
  make install DESTDIR="$pkgdir"
  install -Dm0644 LICENCE -t "$pkgdir/usr/share/licenses/$pkgname"

  # setup permissions on video group for /usr/bin/vcgencmd
  install -Dm0644 ../10-raspberrypi-utils.rules "$pkgdir/usr/lib/udev/rules.d/10-raspberrypi-utils.rules"
}
