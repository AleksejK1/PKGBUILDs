# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for chromium GN
#  - patch for chromium to run blink bindings generation single threaded
#  - restrict job count on v7 - RAM constraints

highmem=1

pkgname=qt6-webengine
_pkgver=6.9.0
pkgver=${_pkgver/-/}
pkgrel=6
_chromium=6287641b6a4c06819a3da8c38e088f8f0d6fbc18
arch=(x86_64)
url='https://www.qt.io'
license=(GPL-3.0-only
         LGPL-3.0-only
         LicenseRef-Qt-Commercial
         Qt-GPL-exception-1.0)
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=(alsa-lib
         dbus
         expat
         ffmpeg
         fontconfig
         freetype2
         gcc-libs
         glib2
         glibc
         harfbuzz
         icu
         lcms2
         libdrm
         libevent
         libjpeg-turbo
         libpng
         libtiff
         libwebp
         libx11
         libxcb
         libxcomposite
         libxfixes
         libxkbcommon
         libxkbfile
         libxdamage
         libxext
         libxml2
         libxrandr
         libxslt
         libxtst
         mesa
         minizip
         nspr
         nss
         openjpeg2
         opus
         qt6-base
         qt6-declarative
         qt6-positioning
         qt6-webchannel
         re2
         snappy
         ttf-font
         zlib)
       # pciutils
       # libvpx disabled if VA-API is used
makedepends=(cmake
             git
             gperf
             jsoncpp
             libepoxy
             libpulse
             libva
             libxcursor
             ninja
             nodejs
             perf
             pipewire
             python-html5lib
             qt6-tools
             qt6-websockets)
optdepends=('pipewire: WebRTC desktop sharing under Wayland')
groups=(qt6)
_pkgfn=${pkgname/6-/}
source=(git+https://code.qt.io/qt/$_pkgfn#tag=v$pkgver
        git+https://code.qt.io/qt/qtwebengine-chromium
        0001-ARM-toolchain-fixes.patch
        0002-Run-blink-bindings-generation-single-threaded.patch)
sha256sums=('032e30f7bff59eaaa95b52b55defd5ee793ece98bfc78b8414ea65f770b26fd1'
            'SKIP'
            '772aeba76d16387e28ef629285965109a4bfaa02ca2dd466e9526fe2030773ec'
            'eff4ebf2e714185ef6e36e30c8d2158da6b41a46e3168d06472d35f6c9977aaf')

prepare() {
  cd $_pkgfn
  git submodule init
  git submodule set-url src/3rdparty "$srcdir"/qtwebengine-chromium
  git -c protocol.file.allow=always submodule update

  # NativeSkiaOutputDeviceOpenGL: Fix leaking X11 Pixmap
  # https://bugreports.qt.io/browse/QTBUG-135047
  # https://codereview.qt-project.org/c/qt/qtwebengine/+/634033
  git cherry-pick -n 08f793a6ef5da886e1282a3fb9a2829bd14413b2

  # Add "default" and "gl" ANGLE implementation support to Ozone
  # https://bugreports.qt.io/browse/QTBUG-133570
  # https://codereview.qt-project.org/c/qt/qtwebengine/+/634920
  git cherry-pick -n a56ea1e9cf1004d475932dc954c9cc494fbf32b1

  # Do not list GLX frame buffer configurations unnecessarily
  # https://bugreports.qt.io/browse/QTBUG-135647
  # https://codereview.qt-project.org/c/qt/qtwebengine/+/638638
  git cherry-pick -n 23cb28fa0c21c3205b398eb177dffada62de6cc9

  # NativeSkiaOutputDeviceVulkan: Use minimal set of usage flags for VkImage
  # https://bugreports.qt.io/browse/QTBUG-123607
  # https://codereview.qt-project.org/c/qt/qtwebengine/+/638636
  git cherry-pick -n f88fa0c83c7f0c063475539b327065f8615fe9d7

  # Bump chromium to head of stable branch
  cd src/3rdparty
  [[ -n $_chromium ]] && git checkout $_chromium

  if [[ $CARCH == "armv7h" ]]; then
    export MAKEFLAGS="-j1"
    export ALARM_NINJA_JOBS="1"
  fi
  sed -i 's/"-Wno-unused-function",/"-Wno-unused-function", "-Wno-incompatible-pointer-types",/' src/3rdparty/chromium/third_party/xnnpack/BUILD.gn
  cd src/3rdparty
  patch -p1 -i ${srcdir}/0001-ARM-toolchain-fixes.patch
  patch -p1 -i ${srcdir}/0002-Run-blink-bindings-generation-single-threaded.patch
}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS \
    -DCMAKE_TOOLCHAIN_FILE=/usr/lib/cmake/Qt6/qt.toolchain.cmake \
    -DQT_FEATURE_webengine_system_ffmpeg=ON \
    -DQT_FEATURE_webengine_system_icu=ON \
    -DQT_FEATURE_webengine_system_libevent=ON \
    -DQT_FEATURE_webengine_system_re2=ON \
    -DQT_FEATURE_webengine_proprietary_codecs=ON \
    -DQT_FEATURE_webengine_kerberos=ON \
    -DQT_FEATURE_webengine_webrtc_pipewire=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$srcdir"/${_pkgfn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium
}
