# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Jkkyll Wu <adaptee at gmail [dot] com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

# ALARM: John <therealgraysky AT proton DOT me>
#  - add conflicts arrays for the respective qbittorrent-dev packages
#  - for the nox package optionally install qt6-base-headless via the libQt6Core.so.6
#  dep which allows the headless client fewer depends/smaller rootfs footprint

pkgbase=qbittorrent-dev
pkgname=(qbittorrent-dev
         qbittorrent-dev-nox)
pkgver=5.1.0rc1
pkgrel=2
arch=(x86_64)
url='https://www.qbittorrent.org'
license=(GPL-2.0-or-later
         GPL-3.0-or-later)
depends=(gcc-libs
         glibc
         libtorrent-rasterbar
         openssl
         zlib)
makedepends=(boost
             cmake
             git
             qt6-base
             qt6-svg
             qt6-tools)
optdepends=('python: needed for torrent search tab')
source=(git+https://github.com/qbittorrent/qBittorrent/#tag=release-$pkgver?signed)
sha256sums=('47e2b9414a7073f3127895a58cc8ad1f71c780ab6f6f38c3d88989924350e578')
validpgpkeys=('D8F3DA77AAC6741053599C136E4A2D025B7CC9A2') # sledgehammer999 <sledgehammer999@qbittorrent.org>

build() {
  cmake -B build -S qBittorrent \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build

  cmake -B build-nox -S qBittorrent \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGUI=OFF \
    -DSYSTEMD=ON
  cmake --build build-nox
}

package_qbittorrent-dev() {
  pkgdesc='An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar, dev version'
  depends+=(qt6-svg hicolor-icon-theme qt6-base)
  conflicts=(qbittorrent)
  provides=(qbittorrent)

  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 qBittorrent/COPYING -t "$pkgdir"/usr/share/licenses/${pkgname/-dev}
}

package_qbittorrent-dev-nox() {
  pkgdesc='An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar, w/o gui, dev version'
  depends+=(libQt6Core.so.6)
  conflicts=(qbittorrent-nox)
  provides=(qbittorrent-nox)

  DESTDIR="$pkgdir" cmake --install build-nox
  install -Dm644 qBittorrent/COPYING -t "$pkgdir"/usr/share/licenses/${pkgname/-dev}
}
