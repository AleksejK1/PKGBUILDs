# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Co-Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to disable LTO

pkgname=libqtxdg
pkgver=4.1.0
pkgrel=5
pkgdesc="Library providing freedesktop.org XDG specs implementations for Qt"
arch=("x86_64")
url="https://github.com/lxqt/$pkgname"
license=("LGPL" "custom")
depends=("libsm" "libxkbcommon-x11" "qt6-base" "qt6-svg")
makedepends=("cmake" "lxqt-build-tools")
checkdepends=("xorg-server-xvfb")
optdepends=(
  'xterm: the default terminal for running .desktop files'
)
provides=("libQt6Xdg.so" "libQt6XdgIconLoader.so")
source=(
  "https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
  "https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
  https://github.com/lxqt/libqtxdg/commit/35ce74f1.patch
  '0001-disable-LTO.patch'
)
sha256sums=('0604d397d9561a6a6148930a2b131f2bdee86cec6cca304f7513a8ec7b8e8809'
            'SKIP'
            '81a0182f7a9adb726c4b052616039d3211fe9bacd129d1d5b12344db54582462'
            '36061244c5f0822a9724f8d4a614673a52a6bd95979f7b0b746e206319ccef5f')
validpgpkeys=(
  "169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
  "7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
  "19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # Pedram Pourang <tsujan2000@gmail.com>
)

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../35ce74f1.patch # Adapt to Qt 6.9
  patch -p1 -i ../0001-disable-LTO.patch
}

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=ON
  make
}

check() {
  cd build

  xvfb-run --auto-display make test
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$pkgname-$pkgver"/{AUTHORS,COPYING,Digia-Qt-LGPL-Exception-1.1} \
    -t "$pkgdir"/usr/share/licenses/$pkgname
}
