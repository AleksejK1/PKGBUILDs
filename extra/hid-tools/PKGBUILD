# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on python-pypandoc

pkgname=hid-tools
pkgver=0.7
pkgrel=3
arch=('any')
url='https://gitlab.freedesktop.org/libevdev/hid-tools'
license=('GPL2')
pkgdesc='Python scripts to manipulate HID data'
depends=('python-parse' 'python-pyudev' 'python-libevdev' 'python-click' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-hatchling' 'python-sphinx' 'python-sphinx_rtd_theme' 'python-sphinxcontrib-apidoc')
source=("$url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz"
	"fix-sphinx.patch")
sha512sums=('9a9bd42ac0a78455e7c5c472b954ac6761c656d51dc9667def50fd61311280d435b227c5b9519d05287063bbdfad3930598a9f0a8e4ffe4cc7cc1cda37c4462c'
            'de6821177ddda48ebd9183f7d9f159797f36c1e7b0f0a08066994322999a58560975208ac6b3702f6022e18ee1f6f1572cdfad72c27aadc649e0c5eb8e5d8ef5')

prepare() {
  cd $pkgname-$pkgver

  patch -Np1 -i ${srcdir}/fix-sphinx.patch
}


build() {
  cd $pkgname-$pkgver

  python -m build -wnx

  cd doc/source

  sphinx-build -b html . _build
}

package() {
  cd $pkgname-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl

  # Install documentation
  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname/html
  cp -r -a --no-preserve=ownership doc/source/_build/* "$pkgdir"/usr/share/doc/$pkgname/html
}
